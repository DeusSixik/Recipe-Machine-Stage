plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "curse.maven:sdm-stages-1082036:7142645"
    modImplementation "curse.maven:crafttweaker-239197:6929483"

    modRuntimeOnly("mezz.jei:jei-1.21.1-neoforge:19.25.0.323")

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-neoforge:16.0.799"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-neoforge:16.0.799"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-neoforge:16.0.799"

    implementsMods(delegate)
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

def implementsMods(DependencyHandler deps) {

    //Applied Energistics 2
    deps.modCompileOnly "curse.maven:applied-energistics-2-223794:7027323"
    deps.modCompileOnly "curse.maven:guideme-1173950:7127444"

    //Apotheosis
    deps.modCompileOnly "curse.maven:apotheosis-313970:6926309"
    deps.modCompileOnly "curse.maven:apothic-attributes-898963:6751650"
    deps.modCompileOnly "curse.maven:apothic-enchanting-1063926:6926290"
    deps.modCompileOnly "curse.maven:apothic-spawners-986583:6751589"
    deps.modCompileOnly "curse.maven:placebo-283644:6926281"

    //Born in Chaos
    deps.modCompileOnly "curse.maven:born-in-chaos-686437:6833966"
    deps.modCompileOnly "curse.maven:geckolib-388172:7023453"

    //Create
    deps.modImplementation("com.tterrag.registrate:Registrate:${registrate_version}")
    deps.modImplementation("com.simibubi.create:create-$minecraft_version:$create_version:slim") { transitive = false }
    deps.modImplementation("net.createmod.ponder:Ponder-NeoForge-$minecraft_version:$ponder_version")
    deps.modCompileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-$minecraft_version:$flywheel_version")
    deps.modRuntimeOnly("dev.engine-room.flywheel:flywheel-neoforge-$minecraft_version:$flywheel_version")

}
